apply plugin: 'java'
apply plugin: 'maven'

group = 'com.trunk.rx.json'

def baseVersion = '0.8.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configure(subprojects) {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://oss.jfrog.org/libs-snapshot' }
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group 'com.trunk.rx.json'
    version = baseVersion

    sourceCompatibility = 1.8

    dependencies {
        compile 'io.reactivex:rxjava:1.1.1'
        compile 'org.slf4j:slf4j-api:1.7.19'

        testCompile 'org.testng:testng:6.8'

        testCompile 'org.slf4j:slf4j-simple:1.7.19'
    }

    test {
        beforeTest { descriptor ->
            logger.lifecycle("Running: " + descriptor)
        }
    }

    test {
        useTestNG()
        systemProperty("io.netty.leakDetectionLevel", "paranoid")
        systemProperty("io.LEVEL", "DEBUG")
        systemProperty("rx.LEVEL", "DEBUG")
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom.withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }
                    }.each() {
                        it.scope*.value = 'compile'
                    }
                }
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }
}
